plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

android {
    namespace = "com.veepee.vpcore.link.route"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        compileSdk 33
        aarMetadata {
            minCompileSdk = 21
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.6'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation libs.lang.kotlin

    implementation libs.androidx.core
    implementation libs.androidx.fragment
    implementation libs.androidx.compose.foundation

    testImplementation libs.test.junit
    testImplementation libs.test.robolectric
    testImplementation libs.test.mockito.inline
    testImplementation libs.test.mockito.kotlin
    testImplementation libs.test.androidx.test.core

    testImplementation libs.androidx.compose.ui.test
    testImplementation libs.androidx.compose.ui.test.manifest
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url "https://maven.pkg.github.com/veepee-oss/link-router"
                credentials {
                    username System.getenv('GITHUB_ACTOR')
                    password System.getenv('GITHUB_TOKEN')
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId "com.veepee.vpcore.link-router"
                artifactId "link-router"
                version System.getenv('RELEASE_VERSION')
                from components.release
            }
        }
    }
}

